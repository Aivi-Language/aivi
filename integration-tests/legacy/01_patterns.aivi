@no_prelude
module integrationTests.legacy.patterns
export head, classify, deepName, unwrap, take, zip, depth, flatten, evalExpr
use aivi
use aivi.testing

Option A = None | Some A
Result E A = Err E | Ok A
Tree A = Leaf A | Node (Tree A) (Tree A)
Expr = Num Int | Add Expr Expr | Mul Expr Expr

head =
  | []       => None
  | [x, ...] => Some x

classify = n => n ?
  | 0            => "zero"
  | n when n < 0 => "negative"
  | _            => "positive"

deepName : { data: { user: { profile: { name: String } } } } -> String
deepName = ?
  | { data.user.profile.{ name! } }
  | _ => "unknown"

unwrap =
  | Ok x  => x
  | Err _ => 0

take = (n, xs) => (n, xs) ?
  | (n, _) when n <= 0 => []
  | (_, [])            => []
  | (n, [x, ...rest])  => [x, ...take (n - 1, rest)]

zip = (xs, ys) => (xs, ys) ?
  | ([], _)                  => []
  | (_, [])                  => []
  | ([x, ...xs], [y, ...ys]) => [(x, y), ...zip (xs, ys)]

append = xs ys => xs ?
  | []        => ys
  | [h, ...t] => [h, ...append t ys]

max = a b => if a > b then a else b

depth =
  | Leaf _          => 1
  | Node left right => 1 + max (depth left)(depth right)

flatten =
  | Leaf x          => [x]
  | Node left right => append (flatten left)(flatten right)

evalExpr =
  | Num n   => n
  | Add a b => evalExpr a + evalExpr b
  | Mul a b => evalExpr a * evalExpr b

__test_sentinel = 0

@test
smoke = effect {
  _ <- assert (head [1, 2, 3] == Some 1)
  _ <- assert (head [] == None)
  _ <- assert (classify 0 == "zero")
  _ <- assert (classify (-1) == "negative")
  t = Node (Leaf 1) (Node (Leaf 2) (Leaf 3))
  _ <- assert (depth t == 3)
  _ <- assert (flatten t == [1, 2, 3])
  e = Add (Num 2) (Mul (Num 3) (Num 4))
  _ <- assert (evalExpr e == 14)
}
