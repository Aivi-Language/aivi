module examples.compiler.records_patching
export User, user1, user2, user3, store2, store3, promote
use aivi.testing


User = { name: Text, age: Int, tags: List Text }

append = xs ys => xs ?
  | []        => ys
  | [h, ...t] => [h, ...append t ys]

user1 : User
user1 = { name: "Ada", age: 36, tags: ["dev"] }

user2 = user1 <| {
  age: _ + 1
  tags: append _["vip"]
}

promote = _ <| {
  tags: append _["core"]
}

bumpName = name => "{name}+"

user3 = user2 <| {
  name: bumpName
  age: _ + 2
}

Item = { price: Int, active: Bool }
Store = { items: List Item }

store = {
  items: [
    { price: 10, active: True }
    { price: 20, active: False }
    { price: 30, active: True }
  ]
}

store2 = store <| {
  items[0].price: x => x + 5
}

store3 = store2 <| {
  items[2].active: True
}

__test_sentinel = 0

@test
smoke = effect {
  _ <- assert (user1.age == 36)
  _ <- assert (user2.age == 37)
  _ <- assert (user2.tags == ["dev", "vip"])
  _ <- assert (user3.name == "Ada+")
  _ <- assert (user3.age == 39)
  _ <- assert (store2.items[0].price == 15)
  _ <- assert (store3.items[2].active == True)
}
