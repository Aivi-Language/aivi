module examples.fantasy_land
export Option, isNone
use aivi.testing

class Setoid A = {
  equals: A -> A -> Bool
}

class Functor (F *) = {
  map: (A -> B) -> F A -> F B
}

class Apply (F *) = {
  ap: F (A -> B) -> F A -> F B
}

class Applicative (F *) = {
  of: A -> F A
}

class Chain (F *) = {
  chain: (A -> F B) -> F A -> F B
}

Option A = None | Some A

// v0.1 does not support instance contexts like `instance Setoid A => Setoid (Option A)`,
// so keep this instance concrete.
instance Setoid (Option Int) = {
  equals: a b => (a, b) ?
    | (None, None)     => True
    | (Some x, Some y) => x == y
    | _                => False
}

instance Functor (Option *) = {
  map: f opt => opt ?
    | None   => None
    | Some x => Some (f x)
}

instance Apply (Option *) = {
  ap: f_opt opt => (f_opt, opt) ?
    | (Some f, Some x) => Some (f x)
    | _                => None
}

instance Applicative (Option *) = {
  of: x => Some x
}

instance Chain (Option *) = {
  chain: f opt => opt ?
    | None   => None
    | Some x => f x
}

isNone =
  | None   => True
  | Some _ => False

__test_sentinel = 0

@test
smoke = effect {
  a = Some 3
  b = Some 3
  _ <- assert (Setoid.equals a b == True)
  _ <- assert (Functor.map (_ + 1) a == Some 4)
  _ <- assert (Apply.ap (Some (_ + 2)) (Some 1) == Some 3)
  _ <- assert (Applicative.of 5 == Some 5)
  _ <- assert (Chain.chain (x => Some (x * 2)) (Some 2) == Some 4)
  _ <- assert (isNone None == True)
}
